apply plugin: 'com.android.application'
apply plugin: 'checkstyle'
apply plugin: 'pmd'

checkstyle {
    toolVersion '8.16'
    ignoreFailures false
}

pmd {
    toolVersion '6.8.0'
    ignoreFailures false
}

build.dependsOn 'checkstyle', 'pmd'

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

task checkstyle(type: Checkstyle) {
    configFile file("../config/checkstyle/google_checks.xml")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    exclude '**/test/**'
    exclude '**/androidTest/**'
    ignoreFailures false
    classpath = files()
    showViolations true

    reports {
        xml.enabled = false
        html.enabled = true
    }
}

task pmd(type: Pmd) {
    ruleSets = []
    ruleSetFiles = files("../config/pmd/ruleset.xml")
    ignoreFailures false
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    exclude '**/test/**'
    exclude '**/androidTest/**'

    reports {
        xml.enabled = false
        html.enabled = true
    }
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.affectiva.affdexme"
        minSdkVersion 28
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters 'arm64-v8a'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation 'com.affectiva.android:auto-sdk:1.2.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'lib.kashif:folderpicker:2.4'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.opencsv:opencsv:4.6'
    implementation 'net.sf.supercsv:super-csv:2.4.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
